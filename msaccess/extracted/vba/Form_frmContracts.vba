' Module Name: Form_frmContracts
' Module Type: Document Module
' Lines of Code: 151
' Extracted: 2026-02-04 13:03:36

Option Compare Database
Option Explicit

Dim NewContractInProgress As Boolean

Private Sub ContractID_AfterUpdate()
On Error GoTo ShowMeError
    ' No change in contract ID so exit.
    If ContractID = RememberContractID Then Exit Sub
    ' Update the corresponding billing book and amendment records.
    If Not Me.NewRecord Then
        If MsgBox("Are you sure you want to change this contract number?", vbYesNo, "Verify change") = vbYes Then
            TILLDataBase.Execute "UPDATE qrytblContractsBillingBook SET qrytblContractsBillingBook.ContractID = """ & [ContractID] & """" & "WHERE (((qrytblContractsBillingBook.ContractID)= """ & [RememberContractID] & """));", dbSeeChanges: Call BriefDelay
            TILLDataBase.Execute "UPDATE qrytblContractsAmendments SET qrytblContractsAmendments.ContractID = """ & [ContractID] & """" & "WHERE (((qrytblContractsAmendments.ContractID)= """ & [RememberContractID] & """));", dbSeeChanges: Call BriefDelay
        Else
            ContractID = RememberContractID
        End If
    Else
        FY = SelectFY
        RecordAddedBy = Form_frmMainMenu.UserName
        RecordAddedDate = Format(Now, "mm/dd/yyyy")
    End If
    Exit Sub
ShowMeError:
    MsgBox "Error # " & Str(Err.Number) & " was generated by " & Me.Name & Chr(13) & Err.Description, vbOKOnly, "Error", Err.HelpFile, Err.HelpContext
End Sub

Private Sub ContractPending_Click()
    If ContractPending Then
        ContractID.BackColor = RGB(191, 191, 191): FY.BackColor = RGB(191, 191, 191): ActivityCode.BackColor = RGB(191, 191, 191)
        GPNumber.BackColor = RGB(191, 191, 191): State.BackColor = RGB(191, 191, 191): UnitRate.BackColor = RGB(191, 191, 191)
        Units.BackColor = RGB(191, 191, 191): TotalClients.BackColor = RGB(191, 191, 191): StartDate.BackColor = RGB(191, 191, 191)
        EndDate.BackColor = RGB(191, 191, 191): MaximumObligation.BackColor = RGB(191, 191, 191): TotalUnits.BackColor = RGB(191, 191, 191)
        MaximumObligationAsAmended.BackColor = RGB(191, 191, 191): TotalUnitsAsAmended.BackColor = RGB(191, 191, 191)
    Else
        ContractID.BackColor = RGB(255, 255, 255): FY.BackColor = RGB(255, 255, 255): ActivityCode.BackColor = RGB(255, 255, 255)
        GPNumber.BackColor = RGB(255, 255, 255): State.BackColor = RGB(255, 255, 255): UnitRate.BackColor = RGB(255, 255, 255)
        Units.BackColor = RGB(255, 255, 255): TotalClients.BackColor = RGB(255, 255, 255): StartDate.BackColor = RGB(255, 255, 255)
        EndDate.BackColor = RGB(255, 255, 255): MaximumObligation.BackColor = RGB(255, 255, 255): TotalUnits.BackColor = RGB(255, 255, 255)
        MaximumObligationAsAmended.BackColor = RGB(255, 255, 255): TotalUnitsAsAmended.BackColor = RGB(255, 255, 255)
    End If
End Sub

Private Sub Form_Current()
    If Not Me.NewRecord Then Call ContractPending_Click
    If Len(ContractID) > 0 Or Not IsNull(ContractID) Then   ' There's a contract ID in the field.
        Me.Caption = "Contract: " & ContractID              ' Change the caption.
        ' Update the counts and $$$ amounts.
        TotalClients = DSum("NumberOfClients", "tblContractsBillingBook", "ContractID = """ & ContractID & """ AND FY = " & SelectFY)
        TotalUnits = DSum("Units", "tblContractsBillingBook", "ContractID = """ & ContractID & """ AND FY = " & SelectFY)
        TotalUnitsAsAmended = DSum("UnitsAsAmended", "tblContractsBillingBook", "ContractID = """ & ContractID & """ AND FY = " & SelectFY)
        MaximumObligation = DSum("MaximumObligation", "tblContractsBillingBook", "ContractID = """ & ContractID & """ AND FY = " & SelectFY)
        MaximumObligationAsAmended = DSum("MaximumObligationAsAmended", "tblContractsBillingBook", "ContractID = """ & ContractID & """ AND FY = " & SelectFY)
    Else    ' Blank contract ID.  Set everything else to zero.
        Me.Caption = "Contract": TotalClients = 0: TotalUnits = 0
    End If
    RememberContractID = ContractID ' Remember the current contract ID in case we plan to change it later.
    SearchContractID.SetFocus
End Sub

Private Sub Form_Load()
    SelectFY = Form_frmMainMenu.SelectFY
End Sub

Private Sub ProcessDeleteRecord_Click()
On Error GoTo ShowMeError
    Dim RememberContractID As String
    
    RememberContractID = ContractID
    DoCmd.GoToRecord acDataForm, Me.Name, acFirst

    If MsgBox("Do you really want to delete this entire contract for FY " & FY & " including all billing book entries and amendments?", vbYesNo, "Confirm Deletion") = vbYes Then
        If MsgBox("Are you absolutely sure you want to delete this entire contract for FY " & FY & " including all billing book entries and amendments?", vbYesNo, "Confirm Deletion") = vbYes Then
            ' Delete all amendments.
            SysCmdResult = SysCmd(4, "Archiving amendments.")
            TILLDataBase.Execute "INSERT INTO tblDELETEDContractsAmendments ( FY, ContractID, AmendmentNumber, RecordDeletedDate, RecordDeletedBy, BillingBookNumber, Pending, PendingAmount, PendingUnits, DDSArea, ProgramName, PurposeReason, DateSubmitted, DateApproved, StartDate, EndDate, NumUnits, NewRate, AmendedAmount, AnnualizedAmount, Comments ) " & _
                "SELECT FY, ContractID, AmendmentNumber, """ & _
                Format(Now(), "mm/dd/yyyy") & """ AS RecordDeletedDate, """ & _
                Form_frmMainMenu.UserName & """ AS RecordDeletedBy, " & _
                "BillingBookNumber, Pending, PendingAmount, PendingUnits, DDSArea, ProgramName, PurposeReason, DateSubmitted, DateApproved, StartDate, " & _
                "EndDate, NumUnits, NewRate, AmendedAmount, AnnualizedAmount, Comments FROM tblContractsAmendments " & _
                "WHERE (((tblContractsAmendments.FY)=" & Form_frmContracts.FY & ") AND ((tblContractsAmendments.ContractID)='" & RememberContractID & "'));", dbSeeChanges: Call BriefDelay
            SysCmdResult = SysCmd(4, "Deleting amendments.")
            TILLDataBase.Execute "DELETE * FROM tblContractsAmendments WHERE " & "FY=" & Form_frmContracts.FY & " AND ContractID='" & RememberContractID & "'", dbSeeChanges: Call BriefDelay
            ' Delete all billing books.
            SysCmdResult = SysCmd(4, "Archiving billing book.")
            TILLDataBase.Execute "INSERT INTO tblDELETEDContractsBillingBook ( FY, ContractID, BIllingBookNumber, RecordDeletedDate, RecordDeletedBy, ProgramName, CostCenter, StartDate, EndDate, 07Units, 07Rate, 08Units, 08Rate, 09Units, 09Rate, 10Units, 10Rate, 11Units, 11Rate, 12Units, 12Rate, 01Units, 01Rate, 02Units, 02Rate, 03Units, 03Rate, 04Units, 04Rate, 05Units, 05Rate, 06Units, 06Rate, MaximumObligation, MaximumObligationAsAmended, Units, UnitsAsAmended, BillingRate, NumberOfClients, InternalRate, FundingSource, DDSArea, Staff, Comments )" & _
                "SELECT FY, ContractID, BIllingBookNumber, """ & _
                Format(Now(), "mm/dd/yyyy") & """ AS RecordDeletedDate, """ & _
                Form_frmMainMenu.UserName & """ AS RecordDeletedBy, " & _
                "ProgramName, CostCenter, StartDate, EndDate, " & _
                "[07Units], [07Rate], [08Units], [08Rate], [09Units], [09Rate], [10Units], [10Rate], [11Units], [11Rate], [12Units], [12Rate], " & _
                "[01Units], [01Rate], [02Units], [02Rate], [03Units], [03Rate], [04Units], [04Rate], [05Units], [05Rate], [06Units], [06Rate], " & _
                "MaximumObligation, MaximumObligationAsAmended, Units, UnitsAsAmended, BillingRate, NumberOfClients, InternalRate, FundingSource, DDSArea, " & _
                "Staff, Comments FROM tblContractsBillingBook " & _
                "WHERE tblContractsBillingBook.FY=" & Form_frmContracts.FY & " AND tblContractsBillingBook.ContractID='" & RememberContractID & "';", dbSeeChanges: Call BriefDelay
            SysCmdResult = SysCmd(4, "Deleting billing book.")
            TILLDataBase.Execute "DELETE * FROM tblContractsBillingBook WHERE " & "FY=" & Form_frmContracts.FY & " AND ContractID='" & RememberContractID & "'", dbSeeChanges: Call BriefDelay
            ' Delete the contract.
            SysCmdResult = SysCmd(4, "Archiving contract.")
            TILLDataBase.Execute "INSERT INTO tblDELETEDContracts ( FY, ContractID, RecordDeletedDate, RecordDeletedBy, State, StartDate, EndDate, " & _
                "UnitRate, Units, MaximumObligation, MaximumObligationAsAmended, GPNumber, ActivityCode, TotalClients, TotalUnits, TotalUnitsAsAmended, Pending ) " & _
                "SELECT tblContracts.FY, tblContracts.ContractID, '" & Format(Now(), "mm/dd/yyyy") & "' AS RecordDeletedDate, '" & Form_frmMainMenu.UserName & "' AS RecordDeletedBy, tblContracts.State, tblContracts.StartDate, tblContracts.EndDate, tblContracts.UnitRate, tblContracts.Units, tblContracts.MaximumObligation, tblContracts.MaximumObligationAsAmended, tblContracts.GPNumber, tblContracts.ActivityCode, tblContracts.TotalClients, tblContracts.TotalUnits, tblContracts.TotalUnitsAsAmended, tblContracts.Pending " & _
                "FROM tblContracts WHERE tblContracts.FY=" & Form_frmContracts.FY & " AND tblContracts.ContractID='" & RememberContractID & "';", dbSeeChanges: Call BriefDelay
            SysCmdResult = SysCmd(4, "Deleting contract.")
            TILLDataBase.Execute "DELETE * FROM tblContracts WHERE " & "FY=" & Form_frmContracts.FY & " AND ContractID='" & RememberContractID & "'", dbSeeChanges: Call BriefDelay
            Me.Requery
        End If
    End If
    Exit Sub
ShowMeError:
    MsgBox "Error # " & Str(Err.Number) & " was generated by " & Me.Name & Chr(13) & Err.Description, vbOKOnly, "Error", Err.HelpFile, Err.HelpContext
End Sub

Private Sub SearchContractID_AfterUpdate()
    Dim rst As DAO.Recordset
    
    Set rst = Me.RecordsetClone
    rst.FindFirst "ContractID = """ & SearchContractID & """"
    Me.Bookmark = rst.Bookmark
    Me.SetFocus
    SearchContractID = ""
    SearchContractID.SetFocus
End Sub

Private Sub SelectFY_AfterUpdate()
    Me.Undo
    Form_frmMainMenu.SelectFY = SelectFY
    Me.Requery
End Sub

Private Function RecordNavigation(WhichButton As Integer, Optional ExitForm As Integer = 0) As Boolean
On Error GoTo ShowMeError
    RecordNavigation = True
    ' All good.  Continue.
    If ExitForm = 1 Then Exit Function
    DoCmd.GoToRecord , , WhichButton
    Exit Function
ShowMeError:
    MsgBox "Can't go any further.", vbOKOnly, "ERROR!"
    RecordNavigation = False
End Function

Private Sub Button_NewRecord_Click()
On Error GoTo ShowMeError
    DoCmd.GoToRecord , , acNewRec
    Exit Sub
ShowMeError:
    MsgBox "Error # " & Str(Err.Number) & " was generated by " & Me.Name & Chr(13) & Err.Description, vbOKOnly, "Error", Err.HelpFile, Err.HelpContext
End Sub

